//subprogram containing various routines/algorithms to carry out or values to calculate

Ans->theta //argument passed from main program
If 1=iPart(Ans //rendering routine
Then
	"            "
	For(I,1,4
		Ans+Ans
	End
	sub(Ans,1,187->Str2
	15->H:2->C:dim(|LCNOTE
	If Ans-Z>10
	Then
		Z+10->D
	Else
		Ans->D
	End
	{Z,D,1 //render from top to bottom
	If theta=1.1
	Then
		135->H
		Text(H,1,Str2
		{D,Z,~1 //render from bottom to top
	End
	For(I,Ans(1),Ans(2),Ans(3)
		If I>1
		2+int(|LCNOTE(I-1->C
		If C<=length(Str7) and fPart(|LCNOTE(I
		Then
			If "[xbar]"=sub(Str7,C,1
			C+1->C
			Text(H,1,Str2
			Text(H,1,sub(Str7,C,int(|LCNOTE(I)-(C-1)
		End
		If not(fPart(|LCNOTE(I
		Text(H,1,Str2
		H+12((theta=1)-(theta=1.1->H
	End
	If theta=1 and H<=135
	Then
		For(H,H,135,12
			Text(H,1,Str2
		End
	End
	
	//Scroll Bar:
	If dim(|LCNOTE)>11
	Then
		Line(264,~16,264,~149,0
		133/dim(|LCNOTE
		Line(264,~16-(Z-1)Ans,264,~149+(dim(|LCNOTE)-Z-10)Ans
	End
Else
	//other routines
End
